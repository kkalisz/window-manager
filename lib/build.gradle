apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


def vcsLink = 'https://github.com/kkalisz/window-manager.git'
group = 'pl.kalisz.kamil'
version = '0.1.0-SNAPSHOT'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = 'window-manager'
        versionCode 1
        versionName "0.1.0-SNAPSHOT"

    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

bintray {
    user = binTrayUser
    key = binTrayApiKey
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = 'kamil-kalisz'
        licenses = ['Apache-2.0']
        vcsUrl = vcsLink
        publish = true
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'com.android.support:support-annotations:23.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
}

connectedCheck << {
    println '##teamcity[jacocoReport dataPath=\'lib/build/outputs/code-coverage/connected/coverage.ec\' includes=\'pl.kalisz.kamil*\' classpath=\'-:lib/build/intermediates/classes/androidTest//** +:lib/build/intermediates/classes/debug//**\']'
}
bintrayUpload.dependsOn("install")
